apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
//LAMBDA SUPPORT
apply plugin: 'me.tatarka.retrolambda'
//LOGGING
apply plugin: 'com.jakewharton.hugo'
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        //LOGGING
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'com.sample.signin.app'
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 5000
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '0.5.0'
    }



    buildTypes {

        release {

            /*
            * By default ProGuard optimizations are disabled. If you want to enable them
            * replace proguard-android.txt with proguard-android-optimize.txt but make sure you
            * test thoroughly if you go this route as app may become unstable.
            */
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {

            versionNameSuffix " Debug"
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }
    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }
}


dependencies {
    final PLAY_SERVICES_VERSION = '9.6.1'
    final SUPPORT_LIBRARY_VERSION = '24.2.1'
    final RETROFIT_VERSION = '2.0.0'
    final OKHTTP_VERSION = '3.2.0'
    final DAGGER_VERSION = '2.2'
    final BUTTERKNIFE_VERSION = '8.0.1'

    //GOOGLE AUTH
    compile "com.google.android.gms:play-services-auth:$PLAY_SERVICES_VERSION"
    //FACEBOOK AUTH
    compile 'com.facebook.android:facebook-android-sdk:4.16.0'

    //UI SUPPORT LIBRARY
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:percent:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha9'

    //NETWORKING
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    //NETWORKING - IMAGE DOWNLOAD

    compile 'com.github.bumptech.glide:glide:3.7.0'
    //RX JAVA
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'

    //VIEW INJECTION
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    //GOOGLE AUTOVALUE DEPENDENCIES
    // Replace provided dependency below with official AutoValue once this issue is fixed
    // https://github.com/google/auto/issues/268
    provided 'com.jakewharton.auto.value:auto-value-annotations:1.3'
    apt 'com.google.auto.value:auto-value:1.3'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.4-rc2'
    apt 'com.ryanharter.auto.value:auto-value-gson:0.4.2'
    apt 'com.squareup:javapoet:1.7.0'

    //LOGGING
    compile 'com.jakewharton.timber:timber:4.1.0'

    //DEPENDENCY INJECTION
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}
